CODE	SEGMENT
	ASSUME	CS:CODE

START:	CLI
	CLD
	MOV	AH,42H
	MOV	CH,0C0H
	INT	18H
	MOV	AL,6BH
	OUT	0A2H,AL
	MOV	AL,1
	OUT	68H,AL
	CALL	ALLCLS

	ASSUME	DS:CODE
	MOV	AX,CS
	MOV	DS,AX
	MOV	AH,3DH
	MOV	DX,OFFSET FILENM
	XOR	AL,AL
	INT	21H
	JNC	FILE1
	JMP	MSDOS

	ASSUME	DS:DATA
FILE1:	MOV	BX,DATA
	MOV	DS,BX
	MOV	BX,AX
	MOV	AH,3FH
	MOV	DX,OFFSET FIELD
	MOV	CX,0F000H
	INT	21H
	JNC	FILE2
	MOV	AH,3EH
	INT	21H
	JMP	MSDOS
FILE2:	MOV	AH,3EH
	INT	21H

	XOR	AX,AX
	MOV	DS,AX
	MOV	AX,CS
	MOV	CX,OFFSET KEYINT
	XCHG	AX,DS:[26H]
	XCHG	CX,DS:[24H]
	MOV	DX,DATA
	MOV	DS,DX
	MOV	DATSEG,DX
	MOV	KEYOFS,CX
	MOV	KEYSEG,AX
	MOV	CX,40H
	MOV	AX,DS
	MOV	ES,AX
	MOV	DI,OFFSET KEYTBL
	XOR	AX,AX
	REP	STOSW
	MOV	DI,OFFSET BULINF
	MOV	CX,100H
	REP	STOSW
	MOV	CNNINF,0
	CALL	MAP
	MOV	DSPSEG,0A100H
	MOV	MANX,1080H
	MOV	MANY,1080H
	MOV	RAY,0
	MOV	BULADD,OFFSET BULINF
	MOV	FIREOK,0
	MOV	LTRNOK,0
	MOV	MAPFL,0
	MOV	MAPRES,0
	STI
MLOOP:	TEST	KEYTBL,0FFH
	JZ	MAIN1
	CALL	DSPMAP
	JMP	MAIN2
MAIN1:	CALL	CLS
	CALL	DSPBIL
	CALL	DSPBUL
	CALL	DSPCNN
	;CALL   DSPENM
	CALL	CHGDSP
MAIN2:	MOV	CX,2000H
WLOOP:	LOOP	WLOOP
	CALL	MOVE
	CALL	MOVBUL
	CALL	MOVCNN
	CALL	FIRE
	CALL	CANNON
	CALL	RANDOM
	INC	GCOUNT
	MOV	AL,KEYTBL+60H
	AND	AL,AL
	JZ	MLOOP
STOP:	MOV	AL,KEYTBL+60H
	AND	AL,AL
	JNZ	STOP
	CLI
	MOV	CX,KEYOFS
	MOV	AX,KEYSEG
	XOR	DX,DX
	MOV	DS,DX
	MOV	DS:[24H],CX
	MOV	DS:[26H],AX
	MOV	AL,70H
	OUT	62H,AL
	XOR	AL,AL
	OUT	60H,AL
	OUT	60H,AL
MSDOS:	MOV	AH,4CH
	INT	21H

ALLCLS	PROC	NEAR
	XOR	AX,AX
	MOV	DI,0A000H
	MOV	ES,DI
	MOV	DI,AX
	MOV	CX,3FA0H/2
	REP	STOSW
	OUT	0A6H,AL
	CALL	GCLS
	MOV	AL,1
	OUT	0A6H,AL
	CALL	GCLS
	RET
ALLCLS	ENDP

GCLS	PROC	NEAR
	XOR	AX,AX
	MOV	CX,0A800H
	MOV	ES,CX
	MOV	DI,AX
	MOV	CX,8000H
	REP	STOSW
	MOV	CX,0B800H
	MOV	ES,CX
	MOV	CX,4000H
	REP	STOSW
	RET
GCLS	ENDP

CLS	PROC	NEAR
	MOV	ES,DSPSEG
	MOV	DI,2000H
	XOR	AX,AX
	MOV	CX,13*80
	REP	STOSW
	XOR	DI,DI
	MOV	CX,13*80
	REP	STOSW
	MOV	SI,OFFSET GRDATA
GR1:	LODSW
	MOV	BP,AX
	MOV	BL,RAY
	ADD	BL,32
	XOR	BH,BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	IMUL	BP
	MOV	AL,80H
	SAR	DX,1
	RCR	AL,1
	SAR	DX,1
	RCR	AL,1
	SAR	DX,1
	RCR	AL,1
	ADD	AL,BYTE PTR MANX-1
	MOV	TEMP,AL
	MOV	CX,MANX
	ADC	CX,DX
	MOV	BL,RAY
	SUB	BL,32
	XOR	BH,BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	IMUL	BP
	MOV	AL,80H
	SAR	DX,1
	RCR	AL,1
	SAR	DX,1
	RCR	AL,1
	SAR	DX,1
	RCR	AL,1
	ADD	AL,BYTE PTR MANY-1
	MOV	TEMP+1,AL
	MOV	BP,MANY
	ADC	BP,DX
	LODSW
	PUSH	SI
	MOV	SI,AX
	MOV	BL,RAY
	XOR	BH,BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	NEG	AX
	IMUL	SI
	MOV	AX,DX
	MOV	BL,RAY
	ADD	BL,64
	XOR	BH,BH
	ADD	BX,BX
	MOV	BX,[BX]+SINTBL
	XCHG	AX,BX
	IMUL	SI
	MOV	AL,DH
	CBW
	MOV	SI,AX
	MOV	AL,BH
	CBW
	XCHG	AX,BP
	XCHG	AX,DX
	MOV	AH,BL
	MOV	TEMP+2,80
GR2:	MOV	BH,DH
	MOV	BL,CH
	SHL	BL,1
	SHR	BX,1
	MOV	BL,[BX]
	MOV	BH,41H
	DEC	BL
	JS	GRCL0
	JZ	GRCL1
	DEC	BL
	DEC	BL
	JS	GRCL2
	JNZ	GRCL4
	MOV	BL,0B1H
	JMP	GR3
GRCL2:	MOV	BL,91H
	JMP	GR3
GRCL0:	MOV	BL,31H
	JMP	GR3
GRCL4:	MOV	BH,5AH
GRCL1:	MOV	BL,51H
GR3:	MOV	ES:[DI],BH
	MOV	ES:[DI+2000H],BL
	INC	DI
	INC	DI
	ADD	TEMP,AH
	ADC	CX,BP
	ADD	TEMP+1,AL
	ADC	DX,SI
	DEC	TEMP+2
	JNZ	GR2
	POP	SI
	CMP	DI,25*160
	JZ	GR6
	JMP	GR1
GR6:	RET
CLS	ENDP

DSPBIL	PROC	NEAR
	XOR	AX,AX
	MOV	CX,DS
	MOV	ES,CX
	MOV	DI,OFFSET BILCHK
	MOV	CX,40H
	REP	STOSW
	MOV	DI,OFFSET BOTTOM
	MOV	CX,160
	MOV	AX,3480H
	REP	STOSW
	MOV	DI,OFFSET SCRN3D
	MOV	CX,2000
	REP	STOSW
	MOV	BL,BYTE PTR MANX+1
	MOV	BH,BYTE PTR MANY+1
	SUB	BL,15
	SUB	BH,15
	SHL	BL,1
	SHR	BX,1
	LEA	SI,[BX]+FIELD2
	MOV	CH,31
DB1:	MOV	CL,31
DB14:	LODSB
	AND	AL,AL
	JNS	DB11
DB13:	JMP	DB9
DB11:	XOR	BH,BH
	MOV	BL,AL
	MOV	AL,[BX]+BILCHK
	AND	AL,AL
	JZ	DB12
	JMP	DB9
DB12:	MOV	[BX]+BILCHK,1
	ADD	BX,BX
	ADD	BX,BX
	ADD	BX,BX
	ADD	BX,OFFSET BILDAT
	MOV	BILADD,BX
	XOR	AL,AL
	MOV	AH,[BX+2]
	MOV	BLINEF,AL
	SUB	AX,MANX
	JBE	DB2
	MOV	BILX1,AX
	OR	BLINEF,1
	XOR	AL,AL
	MOV	AH,[BX+4]
	INC	AH
	SUB	AX,MANX
	MOV	BILX2,AX
	JMP	DB4
DB2:	MOV	BILX2,AX
	XOR	AL,AL
	MOV	AH,[BX+4]
	INC	AH
	SUB	AX,MANX
	JAE	DB3
	OR	BLINEF,1
DB3:	MOV	BILX1,AX
DB4:	XOR	AL,AL
	MOV	AH,[BX+3]
	SUB	AX,MANY
	JBE	DB5
	MOV	BILY1,AX
	OR	BLINEF,2
	XOR	AL,AL
	MOV	AH,[BX+5]
	INC	AH
	SUB	AX,MANY
	MOV	BILY2,AX
	JMP	DB7
DB5:	MOV	BILY2,AX
	XOR	AL,AL
	MOV	AH,[BX+5]
	INC	AH
	SUB	AX,MANY
	JAE	DB6
	OR	BLINEF,2
DB6:	MOV	BILY1,AX
DB7:	PUSH	CX
	PUSH	SI
	TEST	BLINEF,1
	JZ	DB8
	CALL	BYLINE
DB8:	TEST	BLINEF,2
	JZ	DB15
	CALL	BXLINE
DB15:	POP	SI
	POP	CX
DB9:	DEC	CL
	JZ	DB16
	JMP	DB14
DB16:	ADD	SI,128-31
	DEC	CH
	JZ	DB10
	JMP	DB1
DB10:	MOV	SI,OFFSET BOTTOM
	MOV	AX,DS
	MOV	ES,AX
	MOV	DS,DSPSEG
DR13:	MOV	AX,ES:[SI]
	CMP	AX,3480H
	JE	DR25
	CMP	AH,100
	JB	DR14
	MOV	CL,24
	MOV	AL,0FH
	JMP	DR15
DR14:	MOV	CL,AH
	SHR	CL,1
	SHR	CL,1
	MOV	BL,AH
	AND	BL,3
	XOR	BH,BH
	MOV	AL,CS:[BX]+LNEDDT
DR15:	MOV	DI,SI
	SUB	DI,OFFSET BOTTOM
	MOV	DX,ES:[SI+160]
	AND	DX,DX
	JNS	DR20
	XOR	CH,CH
	MOV	AH,0FH
	JMP	DR21
DR20:	MOV	BL,DH
	AND	BL,3
	XOR	BH,BH
	MOV	AH,CS:[BX]+LNSTDT
	SHR	DX,1
	SHR	DX,1
	MOV	CH,DH
	XOR	DL,DL
	SHR	DX,1
	ADD	DI,DX
	SHR	DX,1
	SHR	DX,1
	ADD	DI,DX
DR21:	MOV	DX,160
	MOV	BH,0F1H
	SUB	CL,CH
	JZ	DR24
	XOR	CH,CH
	MOV	[DI],AH
	MOV	[DI+2000H],BH
	MOV	AH,0FH
	JMP	DR23
DR22:	MOV	[DI],AH
	MOV	[DI+2000H],BH
DR23:	ADD	DI,DX
	LOOP	DR22
DR24:	AND	AL,AH
	MOV	[DI],AL
	MOV	[DI+2000H],BH
DR25:	INC	SI
	INC	SI
	CMP	SI,OFFSET BOTTOM+160
	JZ	DR26
	JMP	DR13
DR26:	MOV	AX,ES
	MOV	DS,AX
	RET
LNSTDT	DB	0FH,0EH,0CH,08H
LNEDDT	DB	1,3,7,0FH
DSPBIL	ENDP

CHGDSP	PROC	NEAR
	XOR	AL,AL
	OUT	0A4H,AL
	MOV	MAPFL,AL
	MOV	AL,70H
	OUT	62H,AL
	MOV	AX,DSPSEG
	OUT	60H,AL
	SHR	AX,1
	SHR	AL,1
	SHR	AL,1
	SHR	AL,1
	SHR	AL,1
	OUT	60H,AL
	XOR	DSPSEG,100H
	RET
CHGDSP	ENDP

BYLINE	PROC	NEAR
	MOV	AX,BILX1
	MOV	CX,BILY1
	CALL	ATN
	MOV	BETA1,AL
	MOV	AX,BILX1
	MOV	CX,BILY2
	CALL	ATN
	MOV	BETA2,AL
	CMP	AL,BETA1
	JNS	YL1
	XCHG	AL,BETA1
	MOV	BETA2,AL
	MOV	AX,BILY1
	XCHG	AX,BILY2
	MOV	BILY1,AX
	OR	BLINEF,10H
YL1:	OR	BLINEF,8
	MOV	AX,WORD PTR BETA1
	SUB	AL,RAY
	SUB	AH,RAY
	MOV	WORD PTR PHAI1,AX
	SUB	AL,32
	CMP	AL,64
	JBE	YL2
	SUB	AH,160
	CMP	AH,64
	JBE	YL2
	SUB	AL,33
	JS	YL3
	ADD	AH,96
	JS	YL3
YL2:	RET
YL3:	MOV	BL,PHAI1
	CMP	BL,-31
	JNS	YL6
	MOV	BL,RAY
	ADD	BL,32
	XOR	BH,BH
	ADD	BX,BX
	MOV	BX,[BX]+SINTBL
	AND	BX,BX
	JNS	YL4
	NEG	BX
YL4:	MOV	AX,BILX1
	AND	AX,AX
	JNS	YL5
	NEG	AX
YL5:	MOV	DX,0B505H
	MUL	DX
	ADD	BX,BX
	MOV	AX,BX
	MOV	BX,DX
	XOR	DX,DX
	MOV	DSPX1,DL
	DIV	BX
	MOV	DSPY1,AX
	JMP	YL13
YL6:	XOR	BH,BH
	MOV	DL,[BX]+TANTB2
	SHR	DL,1
	MOV	DSPX1,DL
	ADD	BL,40H
	ADD	BX,BX
	MOV	BX,[BX]+SINTBL
	MOV	AL,BETA1
	ADD	AL,32
	TEST	AL,40H
	JZ	YL10
	MOV	AX,BILY1
	AND	AX,AX
	JNS	YL9
	NEG	AX
YL9:	MUL	BX
	MOV	BL,BETA1
	JMP	YL11
YL10:	MOV	AX,BILX1
	AND	AX,AX
	JNS	YL14
	NEG	AX
YL14:	MUL	BX
	MOV	BL,BETA1
	ADD	BL,40H
YL11:	XOR	BH,BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	AND	AX,AX
	JNS	YL12
	NEG	AX
YL12:	MOV	BX,DX
	XOR	DX,DX
	DIV	BX
	MOV	DSPY1,AX
	TEST	BLINEF,10H
	JNZ	YL13
	OR	BLINEF,4
YL13:	MOV	BL,PHAI2
	XOR	BH,BH
	CMP	BL,32
	JS	YL17
	MOV	BL,RAY
	ADD	BL,96
	ADD	BX,BX
	MOV	BX,[BX]+SINTBL
	AND	BX,BX
	JNS	YL15
	NEG	BX
YL15:	MOV	AX,BILX1
	AND	AX,AX
	JNS	YL16
	NEG	AX
YL16:	MOV	DX,0B505H
	MUL	DX
	ADD	BX,BX
	MOV	AX,BX
	MOV	BX,DX
	XOR	DX,DX
	DIV	BX
	MOV	DSPY2,AX
	MOV	DSPX2,79
	CALL	DRAW
	RET
YL17:	MOV	DL,[BX]+TANTB2
	SHR	DL,1
	MOV	DSPX2,DL
	ADD	BL,40H
	ADD	BX,BX
	MOV	BX,[BX]+SINTBL
	MOV	AL,BETA2
	ADD	AL,32
	TEST	AL,40H
	JZ	YL22
	MOV	AX,BILY2
	AND	AX,AX
	JNS	YL20
	NEG	AX
YL20:	MUL	BX
	MOV	BL,BETA2
	JMP	YL24
YL22:	MOV	AX,BILX1
	AND	AX,AX
	JNS	YL23
	NEG	AX
YL23:	MUL	BX
	MOV	BL,BETA2
	ADD	BL,40H
YL24:	XOR	BH,BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	AND	AX,AX
	JNS	YL25
	NEG	AX
YL25:	MOV	BX,DX
	XOR	DX,DX
	DIV	BX
	MOV	DSPY2,AX
	TEST	BLINEF,10H
	JZ	YL26
	OR	BLINEF,4
YL26:	CALL	DRAW
	RET
BYLINE	ENDP

BXLINE	PROC	NEAR
	TEST	BLINEF,10H
	JZ	XL1
	MOV	AX,BILY2
	MOV	BILY1,AX
	MOV	AL,BETA2
	MOV	BETA1,AL
	MOV	AL,PHAI2
	MOV	PHAI1,AL
	MOV	AX,DSPY2
	MOV	DSPY1,AX
	MOV	AL,DSPX2
	MOV	DSPX1,AL
	AND	BLINEF,0EFH
XL1:	TEST	BLINEF,8
	JNZ	XL2
	MOV	AX,BILX1
	MOV	CX,BILY1
	CALL	ATN
	MOV	BETA1,AL
XL2:	MOV	AX,BILX2
	MOV	CX,BILY1
	CALL	ATN
	MOV	BETA2,AL
	CMP	AL,BETA1
	JNS	XL3
	XCHG	AL,BETA1
	MOV	BETA2,AL
	MOV	AX,BILX1
	XCHG	AX,BILX2
	MOV	BILX1,AX
	MOV	AX,DSPY1
	MOV	DSPY2,AX
	MOV	AL,DSPX1
	MOV	DSPX2,AL
	OR	BLINEF,10H
XL3:	MOV	AX,WORD PTR BETA1
	SUB	AL,RAY
	SUB	AH,RAY
	MOV	WORD PTR PHAI1,AX
	SUB	AL,32
	CMP	AL,64
	JBE	XL4
	SUB	AH,160
	CMP	AH,64
	JBE	XL4
	SUB	AL,33
	JS	XL5
	ADD	AH,96
	JS	XL5
XL4:	RET
XL5:	MOV	BL,PHAI1
	MOV	AH,14H
	AND	AH,BLINEF
	CMP	AH,4
	JNZ	XL6
	JMP	XL17
XL6:	CMP	BL,-31
	JNS	XL9
	MOV	BL,RAY
	SUB	BL,32
	XOR	BH,BH
	ADD	BX,BX
	MOV	BX,[BX]+SINTBL
	AND	BX,BX
	JNS	XL7
	NEG	BX
XL7:	MOV	AX,BILY1
	AND	AX,AX
	JNS	XL8
	NEG	AX
XL8:	MOV	DX,0B505H
	MUL	DX
	ADD	BX,BX
	MOV	AX,BX
	MOV	BX,DX
	XOR	DX,DX
	MOV	DSPX1,DL
	DIV	BX
	MOV	DSPY1,AX
	JMP	XL17
XL9:	XOR	BH,BH
	MOV	DL,[BX]+TANTB2
	SHR	DL,1
	MOV	DSPX1,DL
	ADD	BL,40H
	ADD	BX,BX
	MOV	BX,[BX]+SINTBL
	MOV	AL,BETA1
	ADD	AL,32
	TEST	AL,40H
	JZ	XL13
	MOV	AX,BILY1
	AND	AX,AX
	JNS	XL12
	NEG	AX
XL12:	MUL	BX
	MOV	BL,BETA1
	JMP	XL15
XL13:	MOV	AX,BILX1
	AND	AX,AX
	JNS	XL14
	NEG	AX
XL14:	MUL	BX
	MOV	BL,BETA1
	ADD	BL,40H
XL15:	XOR	BH,BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	AND	AX,AX
	JNS	XL16
	NEG	AX
XL16:	MOV	BX,DX
	XOR	DX,DX
	DIV	BX
	MOV	DSPY1,AX
XL17:	MOV	AH,14H
	AND	AH,BLINEF
	CMP	AH,14H
	JNZ	XL18
	CALL	DRAW
	RET
XL18:	MOV	BL,PHAI2
	XOR	BH,BH
	CMP	BL,32
	JS	XL21
	MOV	BL,RAY
	ADD	BL,32
	ADD	BX,BX
	MOV	BX,[BX]+SINTBL
	AND	BX,BX
	JNS	XL19
	NEG	BX
XL19:	MOV	AX,BILY1
	AND	AX,AX
	JNS	XL20
	NEG	AX
XL20:	MOV	DX,0B505H
	MUL	DX
	ADD	BX,BX
	MOV	AX,BX
	MOV	BX,DX
	XOR	DX,DX
	DIV	BX
	MOV	DSPY2,AX
	MOV	DSPX2,79
	CALL	DRAW
	RET
XL21:	MOV	DL,[BX]+TANTB2
	SHR	DL,1
	MOV	DSPX2,DL
	ADD	BL,40H
	ADD	BX,BX
	MOV	BX,[BX]+SINTBL
	MOV	AL,BETA2
	ADD	AL,32
	TEST	AL,40H
	JZ	XL25
	MOV	AX,BILY1
	AND	AX,AX
	JNS	XL24
	NEG	AX
XL24:	MUL	BX
	MOV	BL,BETA2
	JMP	XL27
XL25:	MOV	AX,BILX2
	AND	AX,AX
	JNS	XL26
	NEG	AX
XL26:	MUL	BX
	MOV	BL,BETA2
	ADD	BL,40H
XL27:	XOR	BH,BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	AND	AX,AX
	JNS	XL28
	NEG	AX
XL28:	MOV	BX,DX
	XOR	DX,DX
	DIV	BX
	MOV	DSPY2,AX
	CALL	DRAW
	RET
BXLINE	ENDP

DRAW	PROC	NEAR
	MOV	CX,DSPY1
	MOV	SI,BILADD
	TEST	CH,0F8H
	JNZ	DR17
	SHL	CX,1
	SHL	CX,1
	SHL	CX,1
	SHL	CX,1
	MOV	AL,[SI]
	XOR	AH,AH
	MUL	CX
	ADD	CH,52
	AND	DX,DX
	JNZ	DR18
	AND	AH,AH
	JS	DR18
	NEG	AX
	ADD	AH,52
	MOV	DI,AX
	JMP	DRW1
DR17:	MOV	CX,0B3FFH
DR18:	MOV	DI,0B401H
DRW1:	MOV	BP,DSPY2
	TEST	BP,0F800H
	JNZ	DR19
	SHL	BP,1
	SHL	BP,1
	SHL	BP,1
	SHL	BP,1
	MOV	AL,[SI]
	XOR	AH,AH
	MUL	BP
	ADD	BP,52*256
	AND	DX,DX
	JNZ	DRW2
	AND	AH,AH
	JS	DRW2
	NEG	AX
	ADD	AH,52
	JMP	DR16
DR19:	MOV	BP,0B3FFH
DRW2:	MOV	AX,0B401H
DR16:	MOV	BL,DSPX2
	SUB	BL,DSPX1
	JZ	DRW3
	XOR	BH,BH
	SUB	AX,DI
	CWD
	IDIV	BX
	MOV	SI,AX
	MOV	AX,BP
	SUB	AX,CX
	CWD
	IDIV	BX
	MOV	BP,AX
DRW3:	INC	BL
	MOV	DL,DSPX1
	XOR	DH,DH
	XCHG	DX,DI
	ADD	DI,DI
	ADD	CX,80H
	ADD	DX,80H
DR4:	PUSH	DX
	PUSH	BX
	PUSH	DI
	CMP	CX,[DI]+BOTTOM
	JBE	DR5
	MOV	[DI]+BOTTOM,CX
DR5:	CMP	DX,[DI]+BOTTOM+160
	JNS	DRW6
	MOV	[DI]+BOTTOM+160,DX
DRW6:	AND	DX,DX
	JNS	DRW7
	XOR	BH,BH
	JMP	DR8
DRW7:	SHR	DX,1
	SHR	DX,1
	MOV	BH,DH
	XOR	DL,DL
	SHR	DX,1
	ADD	DI,DX
	SHR	DX,1
	SHR	DX,1
	ADD	DI,DX
DR8:	CMP	CH,100
	JB	DR9
	MOV	BL,25
	JMP	DR10
DR9:	MOV	BL,CH
	SHR	BL,1
	SHR	BL,1
	INC	BL
DR10:	SUB	BL,BH
	MOV	DX,160
DR11:	CMP	CX,[DI]+SCRN3D
	JBE	DR12
	MOV	[DI]+SCRN3D,CX
DR12:	ADD	DI,DX
	DEC	BL
	JNZ	DR11
	POP	DI
	POP	BX
	POP	DX
	ADD	DX,SI
	ADD	CX,BP
	INC	DI
	INC	DI
	DEC	BL
	JNZ	DR4
	RET
DRAW	ENDP

ATN	PROC	NEAR
	AND	AX,AX
	JS	AT1
	AND	CX,CX
	JS	AT2
	CALL	ATN2
	RET
AT2:	NEG	CX
	CALL	ATN2
	NEG	AL
	RET
AT1:	NEG	AX
	AND	CX,CX
	JS	AT3
	CALL	ATN2
	NEG	AL
	ADD	AL,80H
	RET
AT3:	NEG	CX
	CALL	ATN2
	ADD	AL,80H
	RET
ATN2:	CMP	AX,CX
	JB	AT21
	JE	AT22
	MOV	DX,CX
	MOV	CX,AX
	XOR	AX,AX
	DIV	CX
	JMP	ATN3
AT21:	MOV	DX,AX
	XOR	AX,AX
	DIV	CX
	CALL	ATN3
	NEG	AL
	ADD	AL,40H
	RET
AT22:	MOV	AL,32
	RET
ATN3:	MOV	SI,OFFSET TANTBL
	MOV	BX,OFFSET TANTBL+64
AT31:	LEA	DI,[BX+SI]
	SHR	DI,1
	ADD	DI,8000H
	AND	DI,0FFFEH
	CMP	SI,DI
	JE	AT32
	CMP	AX,[DI]
	JA	AT34
	JE	AT33
	MOV	BX,DI
	JMP	AT31
AT34:	MOV	SI,DI
	JMP	AT31
AT32:	MOV	DX,AX
	SUB	DX,[DI]
	SUB	AX,[DI+2]
	NEG	AX
	CMP	AX,DX
	JAE	AT33
	INC	DI
	INC	DI
AT33:	SUB	DI,OFFSET TANTBL
	MOV	AX,DI
	SHR	AL,1
	RET
ATN	ENDP

MOVE	PROC	NEAR
	MOV	AL,KEYTBL+3EH
	MOV	AH,KEYTBL+40H
	CMP	AX,0FFH
	JNZ	MV0
	TEST	LTRNOK,0FFH
	JNZ	MV2
	SUB	RAY,40H
	MOV	LTRNOK,AL
	JMP	MV02
MV0:	MOV	LTRNOK,0
	CMP	AX,0FF00H
	JNZ	MV01
	TEST	RTRNOK,0FFH
	JNZ	MV2
	ADD	RAY,40H
	MOV	RTRNOK,AH
	JMP	MV02
MV01:	MOV	RTRNOK,0
MV02:	MOV	AL,KEYTBL+42H
	MOV	AH,KEYTBL+44H
	CMP	AX,0FFH
	JNZ	MV1
	DEC	RAY
	DEC	RAY
	JMP	MV2
MV1:	CMP	AX,0FF00H
	JNZ	MV2
	INC	RAY
	INC	RAY
MV2:	MOV	AL,KEYTBL+4BH
	AND	AL,1
	MOV	AH,KEYTBL+46H
	SHL	AH,1
	RCL	AL,1
	MOV	AH,KEYTBL+48H
	SHL	AH,1
	RCL	AL,1
	MOV	AH,KEYTBL+43H
	SHL	AH,1
	RCL	AL,1
	XOR	CL,CL
	CMP	AL,1
	JZ	MV3
	MOV	CL,80H
	CMP	AL,8
	JZ	MV3
	MOV	CL,40H
	CMP	AL,2
	JZ	MV3
	MOV	CL,0C0H
	CMP	AL,4
	JZ	MV3
	MOV	CL,20H
	CMP	AL,3
	JZ	MV3
	MOV	CL,0E0H
	CMP	AL,5
	JZ	MV3
	MOV	CL,60H
	CMP	AL,0AH
	JZ	MV3
	MOV	CL,0A0H
	CMP	AL,0CH
	JZ	MV3
	JMP	MV4
MV3:	ADD	CL,RAY
	MOV	BL,BYTE PTR MANX+1
	MOV	BH,BYTE PTR MANY+1
	SHL	BL,1
	SHR	BX,1
	CMP	[BX]+FIELD,3
	JNZ	MV5
	MOV	AL,BYTE PTR RND
	SAR	AL,1
	SAR	AL,1
	SAR	AL,1
	AND	AL,0FEH
	ADD	CL,AL
	ADD	RAY,AL
MV5:	MOV	BL,CL
	XOR	BH,BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	SAR	AX,1
	SAR	AX,1
	CWD
	MOV	BL,AH
	MOV	BH,DL
	ADD	AL,BYTE PTR MANY-1
	ADC	BX,MANY
	MOV	BP,BX
	MOV	CH,AL
	ADD	CL,40H
	MOV	BL,CL
	XOR	BH,BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	SAR	AX,1
	SAR	AX,1
	CWD
	MOV	CL,AL
	MOV	AL,AH
	MOV	AH,DL
	ADD	CL,BYTE PTR MANX-1
	ADC	AX,MANX
	MOV	DX,BP
	SUB	AX,20H
	SUB	DX,20H
	CALL	MVCHK
	JZ	MV4
	ADD	AX,40H
	CALL	MVCHK
	JZ	MV4
	ADD	DX,40H
	CALL	MVCHK
	JZ	MV4
	SUB	AX,40H
	CALL	MVCHK
	JZ	MV4
	SUB	DX,20H
	ADD	AX,20H
	MOV	MANX,AX
	MOV	BYTE PTR MANX-1,CL
	MOV	MANY,DX
	MOV	BYTE PTR MANY-1,CH
MV4:	RET
MOVE	ENDP

MVCHK	PROC	NEAR
	MOV	BL,AH
	MOV	BH,DH
	SHL	BL,1
	SHR	BX,1
	TEST	[BX]+FIELD,0FFH
	JZ	MC1
	TEST	[BX]+FIELD2,80H
MC1:	RET
MVCHK	ENDP

KEYINT	PROC	NEAR
	PUSH	DS
	PUSH	AX
	PUSH	BX
	PUSH	CX
	MOV	AX,DATA
	MOV	DS,AX
	MOV	CX,20
KI1:	LOOP	KI1
	MOV	AL,16H
	OUT	43H,AL
	NOP
	IN	AL,41H
	IN	AL,41H
	MOV	CX,500
KI2:	LOOP	KI2
	MOV	BX,OFFSET KEYTBL
	MOV	BL,AL
	AND	BL,7FH
	CBW
	NOT	AH
	MOV	[BX],AH
	MOV	AL,20H
	OUT	0,AL
	POP	CX
	POP	BX
	POP	AX
	POP	DS
	IRET
KEYINT	ENDP

PSET	PROC	NEAR
	CALL	PSET2
	OR	ES:[BX],CL
	RET
PSET	ENDP

PRESET	PROC	NEAR
	CALL	PSET2
	NOT	CL
	AND	ES:[BX],CL
	RET
PRESET	ENDP

PSET2	PROC	NEAR
	MOV	SI,BX
	ADD	BX,BX
	ADD	BX,BX
	ADD	BX,SI
	ADD	BX,BX
	ADD	BX,BX
	ADD	BX,BX
	ADD	BX,BX
	MOV	SI,CX
	SHR	CX,1
	SHR	CX,1
	SHR	CX,1
	ADD	BX,CX
	AND	SI,7
	MOV	CL,CS:PSETDT[SI]
	RET
PSETDT	DB	80H,40H,20H,10H,8,4,2,1
PSET2	ENDP

DSPENM	PROC	NEAR
	MOV	SI,OFFSET ENMINF
	MOV	ES,DSPSEG
DE1:	MOV	AX,[SI]
	SUB	AX,MANX
	CMP	AH,-16
	JL	DE2
	CMP	AH,16
	JL	DE3
DE2:	ADD	SI,4
	CMP	SI,OFFSET ENMINF+100*4
	JNZ	DE1
	RET
DE3:	MOV	CX,[SI+2]
	SUB	CX,MANY
	CMP	CH,-16
	JL	DE2
	CMP	CH,16
	JGE	DE2
	PUSH	SI
	CALL	ATN
	POP	SI
	MOV	BH,AL
	SUB	AL,RAY
	MOV	BL,AL
	ADD	AL,32
	CMP	AL,64
	JAE	DE2
	PUSH	SI
	CALL	DE5
	POP	SI
	JMP	DE2
DE5:	MOV	CX,BX
	XOR	BH,BH
	ADD	BL,40H
	ADD	BX,BX
	MOV	BX,[BX]+SINTBL
	MOV	AL,CH
	ADD	AL,32
	TEST	AL,40H
	JZ	DE7
	MOV	AX,[SI+2]
	SUB	AX,MANY
	JNS	DE6
	NEG	AX
DE6:	MUL	BX
	MOV	BL,CH
	JMP	DE9
DE7:	MOV	AX,[SI]
	SUB	AX,MANX
	JNS	DE8
	NEG	AX
DE8:	MUL	BX
	MOV	BL,CH
	ADD	BL,40H
DE9:	XOR	BH,BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	AND	AX,AX
	JNS	DE10
	NEG	AX
DE10:	MOV	BX,DX
	XOR	DX,DX
	DIV	BX
	TEST	AH,0F8H
	JNZ	DE11
	SHL	AX,1
	SHL	AX,1
	SHL	AX,1
	SHL	AX,1
	ADD	AX,52*256+80H
	JMP	DE12
DE11:	MOV	AX,0B480H
DE12:	MOV	DSPY1,AX
	MOV	BL,CL
	XOR	BH,BH
	MOV	CL,[BX]+TANTB2
	SUB	AH,39H
	JNB	DE15
	JMP	DE29
DE15:	CMP	AH,0BH
	JA	DE13
	MOV	BL,AH
	ADD	BX,BX
	MOV	SI,[BX]+ENMDAD
	JMP	DE36
DE13:	XOR	BL,BL
	CMP	AH,13H
	JB	DE14
	INC	BL
	CMP	AH,1FH
	JB	DE14
	INC	BL
	CMP	AH,2BH
	JB	DE14
	INC	BL
	CMP	AH,3BH
	JB	DE14
	INC	BL
DE14:	ADD	BX,BX
	MOV	SI,[BX]+ENMDAD+22
DE36:	SHR	CL,1
	JNB	DE16
	MOV	SI,[SI-2]
DE16:	XOR	CH,CH
	ADD	CX,CX
	MOV	DI,CX
	ADD	DI,13*160
	MOV	CX,160
	MOV	BH,0D1H
	LODSW
	MOV	DX,AX
	MOV	BL,DH
	LODSW
	PUSH	AX
	PUSH	DI
DE17:	CMP	DI,13*160
	JB	DE22
	MOV	AX,DSPY1
	CMP	AX,[DI]+BOTTOM-13*160
	JBE	DE21A
	MOV	DH,BL
	MOV	BP,DI
DE18:	LODSB
	AND	AL,AL
	JZ	DE20
	CMP	ES:[BP+2000H],BH
	JE	DE19
	MOV	ES:[BP],AL
	MOV	ES:[BP+2000H],BH
	JMP	DE20
DE19:	OR	ES:[BP],AL
DE20:	ADD	BP,CX
	DEC	DH
	JNZ	DE18
	JMP	DE21
DE21A:	MOV	BP,BX
	AND	BP,0FFH
	ADD	SI,BP
DE21:	DEC	DI
	DEC	DI
	DEC	DL
	JNZ	DE17
DE22:	POP	DI
	INC	DI
	INC	DI
	POP	SI
	LODSB
	AND	AL,AL
	JZ	DE27
	MOV	DL,AL
DE23:	CMP	DI,14*160
	JAE	DE27
	MOV	AX,DSPY1
	CMP	AX,[DI]+BOTTOM-13*160
	JBE	DE26A
	MOV	DH,BL
	MOV	BP,DI
DE24:	LODSB
	AND	AL,AL
	JZ	DE28
	CMP	ES:[BP+2000H],BH
	JE	DE25
	MOV	ES:[BP],AL
	MOV	ES:[BP+2000H],BH
	JMP	DE28
DE25:	OR	ES:[BP],AL
DE28:	ADD	BP,CX
	DEC	DH
	JNZ	DE24
	JMP	DE26
DE26A:	MOV	BP,BX
	AND	BP,0FFH
	ADD	SI,BP
DE26:	INC	DI
	INC	DI
	DEC	DL
	JNZ	DE23
DE27:	RET
DE29:	MOV	DI,CX
	AND	DI,00FEH
	ADD	DI,13*160
	SHR	CL,1
	INC	AH
	JZ	DE32
	INC	AH
	JZ	DE31
	INC	AH
	JZ	DE30
	MOV	AL,1
	JNB	DOT
	MOV	AL,10H
	JMP	DOT
DE30:	MOV	AL,3
	JNB	DOT
	MOV	AL,30H
	JMP	DOT
DE31:	MOV	AL,77H
	JNB	DOT
	MOV	AL,70H
	CALL	DOT
	INC	DI
	INC	DI
	MOV	AL,7
	JMP	DOT
DE32:	JB	DE33
	MOV	AL,7FH
	CALL	DOT
	DEC	DI
	DEC	DI
	CMP	DI,13*160
	MOV	AL,50H
	JAE	DOT
	RET
DE33:	MOV	AL,0F7H
	CALL	DOT
	INC	DI
	INC	DI
	MOV	AL,5
DOT:	MOV	BX,DSPY1
	CMP	BX,[DI]+BOTTOM-13*160
	JBE	DE34
	CMP	BYTE PTR ES:[DI+2000H],0D1H
	JZ	DE35
	MOV	ES:[DI],AL
	MOV	BYTE PTR ES:[DI+2000H],0D1H
DE34:	RET
DE35:	OR	ES:[DI],AL
	RET
DSPENM	ENDP

DSPBUL	PROC	NEAR
	MOV	SI,OFFSET BULINF
	MOV	ES,DSPSEG
DBL1:	MOV	AL,[SI]
	AND	AL,AL
	JZ	DBL2
	MOV	AX,[SI+2]
	SUB	AX,MANX
	CMP	AH,-10
	JL	DBL2
	CMP	AH,10
	JGE	DBL2
	MOV	CX,[SI+6]
	SUB	CX,MANY
	CMP	CH,-10
	JL	DBL2
	CMP	CH,10
	JGE	DBL2
	PUSH	SI
	CALL	DBUL2
	POP	SI
DBL2:	ADD	SI,16
	CMP	SI,OFFSET BULINF+16*32
	JNE	DBL1
DBL12:	RET
DBUL2:	CALL	CALC
	JNB	DBL9
	MOV	AX,DSPY1
	MOV	CX,AX
	SHR	AX,1
	ADD	AX,3480H
	CMP	AH,100
	JAE	DBL9
	ADD	CX,3480H
	MOV	AL,DSPX1
	MOV	BL,AL
	AND	BL,0FEH
	XOR	BH,BH
	CMP	CX,[BX]+BOTTOM
	JBE	DBL9
	MOV	DL,0D1H
	CALL	SPSET
DBL9:	RET
DSPBUL	ENDP

SPSET2	PROC	NEAR
	MOV	CL,AH
	AND	CL,3
	SHR	AL,1
	JNB	SPS1
	ADD	CL,4
SPS1:	MOV	DH,1
	AND	CL,CL
	JZ	SPS2
	SHL	DH,CL
SPS2:	AND	AH,0FCH
	MOV	BL,AH
	XOR	BH,BH
	MOV	DI,BX
	ADD	DI,DI
	ADD	DI,DI
	ADD	DI,BX
	ADD	DI,DI
	ADD	DI,DI
	ADD	DI,DI
	XOR	AH,AH
	ADD	AX,AX
	ADD	DI,AX
	RET
SPSET2	ENDP

SPSET	PROC	NEAR
	CALL	SPSET2
	CMP	ES:[DI+2000H],DL
	JZ	SPS3
	MOV	ES:[DI],DH
	MOV	ES:[DI+2000H],DL
	RET
SPS3:	OR	ES:[DI],DH
	RET
SPSET	ENDP

FIRE	PROC	NEAR
	MOV	AL,FIREOK
	DEC	AL
	JS	FR2
	MOV	FIREOK,AL
FR1:	RET
FR2:	MOV	AL,KEYTBL+34H
	AND	AL,AL
	JZ	FR1
	MOV	SI,BULADD
	MOV	CL,RAY
	XOR	BH,BH
	MOV	[SI+4],CL
	MOV	BL,CL
	MOV	[SI+1],BH
	MOV	[SI+5],BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	MOV	[SI+10],AX
	MOV	AL,AH
	CBW
	SAR	AX,1
	SAR	AX,1
	SAR	AX,1
	ADD	AX,MANY
	MOV	[SI+6],AX
	XOR	BH,BH
	MOV	BL,CL
	ADD	BL,64
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	MOV	[SI+8],AX
	MOV	AL,AH
	CBW
	SAR	AX,1
	SAR	AX,1
	SAR	AX,1
	ADD	AX,MANX
	MOV	[SI+2],AX
	MOV	BYTE PTR [SI],64
	ADD	SI,16
	CMP	SI,OFFSET BULINF+16*32
	JNE	FR3
	MOV	SI,OFFSET BULINF
FR3:	MOV	BULADD,SI
	MOV	FIREOK,1
	RET
FIRE	ENDP

MOVBUL	PROC	NEAR
	MOV	SI,OFFSET BULINF
MBL1:	MOV	AL,[SI]
	DEC	AL
	JNS	MBL4
	JMP	MBL5
MBL4:	MOV	[SI],AL
	MOV	BX,[SI+8]
	MOV	AL,BH
	CBW
	ADD	BL,[SI+1]
	ADC	AX,[SI+2]
	JS	MBL11
	MOV	CX,AX
	MOV	DX,[SI+10]
	MOV	AL,DH
	CBW
	ADD	DL,[SI+5]
	ADC	AX,[SI+6]
	JS	MBL11
	MOV	DH,BL
	MOV	BL,CH
	MOV	BH,AH
	SHL	BL,1
	SHR	BX,1
	TEST	[BX]+FIELD2,80H
	JZ	MBL11
	CMP	CH,[SI+3]
	JZ	MBL3
	CMP	AH,[SI+7]
	JZ	MBL3
	MOV	BL,[SI+3]
	MOV	BH,AH
	SHL	BL,1
	SHR	BX,1
	TEST	[BX]+FIELD2,80H
	JZ	MBL2
	MOV	BL,CH
	MOV	BH,[SI+7]
	SHL	BL,1
	SHR	BX,1
	TEST	[BX]+FIELD2,80H
	JNZ	MBL3
	JMP	MBL6
MBL2:	MOV	BL,CH
	MOV	BH,[SI+7]
	SHL	BL,1
	SHR	BX,1
	TEST	[BX]+FIELD2,80H
	JNZ	MBL6
MBL11:	MOV	BYTE PTR [SI],0
	JMP	MBL5
MBL3:	MOV	[SI+1],DH
	MOV	[SI+2],CX
	MOV	[SI+5],DL
	MOV	[SI+6],AX
MBL5:	ADD	SI,16
	CMP	SI,OFFSET BULINF+16*32
	JZ	MBLT1
	JMP	MBL1
MBLT1:	RET
MBL6:	MOV	BL,[SI+3]
	MOV	BH,[SI+7]
	MOV	[SI+1],DH
	MOV	[SI+2],CX
	MOV	[SI+5],DL
	MOV	[SI+6],AX
	MOV	AL,[SI+4]
	ADD	AL,32
	TEST	AL,40H
	JNZ	MBL8
	XOR	AH,AH
	MOV	AL,[SI+2]
	MOV	CX,[SI+8]
	NEG	AX
	AND	CX,CX
	JNS	MBL7
	NOT	AH
MBL7:	IMUL	WORD PTR [SI+10]
	IDIV	CX
	ADD	AX,[SI+6]
	MOV	BH,AH
	CMP	AH,[SI+7]
	JZ	MBL10
	MOV	BL,[SI+3]
	JMP	MBL10
MBL8:	XOR	AH,AH
	MOV	AL,[SI+6]
	MOV	CX,[SI+10]
	NEG	AX
	AND	CX,CX
	JNS	MBL9
	NOT	AH
MBL9:	IMUL	WORD PTR [SI+8]
	IDIV	CX
	ADD	AX,[SI+2]
	MOV	BL,AH
	CMP	AH,[SI+3]
	JZ	MBL10
	MOV	BH,[SI+7]
MBL10:	SHL	BL,1
	SHR	BX,1
	TEST	[BX]+FIELD2,80H
	JNZ	MBL5
	MOV	BYTE PTR [SI],0
	JMP	MBL5
MOVBUL	ENDP

DSPMAP	PROC	NEAR
	MOV	AX,DSPSEG
	XOR	AX,100H
	MOV	ES,AX
	TEST	MAPFL,1
	JNZ	DMP2
	XOR	DI,DI
	XOR	AX,AX
	MOV	CX,2000
	REP	STOSW
	MOV	AL,1
	OUT	0A4H,AL
	MOV	MAPFL,AL
DMP2:	MOV	DL,0D1H
	TEST	GCOUNT,20H
	JZ	DMP1
	MOV	DL,31H
DMP1:	MOV	AL,BYTE PTR MANX+1
	MOV	AH,BYTE PTR MANY+1
	SUB	AX,0E0EH
	CALL	SPSET2
	MOV	SI,MAPRES
	MOV	BYTE PTR ES:[SI],0
	MOV	ES:[DI],DH
	MOV	ES:[DI+2000H],DL
	MOV	MAPRES,DI
	MOV	AX,0B000H
	MOV	ES,AX
	MOV	AL,1
	OUT	0A6H,AL
	MOV	DI,940
	XOR	AX,AX
	MOV	BL,79
DMP3:	MOV	CX,5
	REP	STOSW
	ADD	DI,70
	DEC	BL
	JNZ	DMP3
	MOV	DI,80H
	MOV	BP,DI
	MOV	BL,RAY
	XOR	BH,BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	SHL	AL,1
	MOV	AL,AH
	CBW
	RCL	AX,1
	MOV	DX,AX
	MOV	BL,RAY
	ADD	BL,64
	XOR	BH,BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	SHL	AL,1
	MOV	AL,AH
	CBW
	RCL	AX,1
	MOV	WORD PTR TEMP,AX
	MOV	TEMP+2,40
DMP4:	MOV	CX,520
	MOV	BX,50
	MOV	AX,DI
	MOV	AL,AH
	CBW
	ADD	CX,AX
	MOV	AX,BP
	MOV	AL,AH
	CBW
	ADD	BX,AX
	CALL	PSET
	ADD	DI,WORD PTR TEMP
	ADD	BP,DX
	DEC	TEMP+2
	JNZ	DMP4
	RET
DSPMAP	ENDP

MAP	PROC	NEAR
	MOV	AL,1
	OUT	0A6H,AL
	MOV	TEMP,4
	MOV	BP,0B800H
MP1:	MOV	ES,BP
	MOV	SI,14*128+14
	XOR	DI,DI
MP2:	MOV	CH,25
MP3:	MOV	CL,4
	XOR	AX,AX
	XOR	DX,DX
MP4:	MOV	BX,0F07H
	TEST	[SI]+FIELD2,80H
	JZ	MP5
	MOV	BL,[SI]
	XOR	BH,BH
	ADD	BX,BX
	MOV	BX,CS:MAPCOL[BX]
MP5:	SHL	AX,1
	SHL	AX,1
	SHL	AX,1
	SHL	AX,1
	SHL	DX,1
	SHL	DX,1
	SHL	DX,1
	SHL	DX,1
	TEST	BL,TEMP
	JZ	MP6
	OR	AL,BH
	CMP	BH,5
	JNZ	MP6
	OR	DL,0FH
MP6:	INC	SI
	DEC	CL
	JNZ	MP4
	XCHG	AH,AL
	XCHG	DH,DL
	MOV	ES:[DI+240],AX
	MOV	ES:[DI+80],AX
	XOR	AX,DX
	MOV	ES:[DI+160],AX
	STOSW
	DEC	CH
	JNZ	MP3
	ADD	SI,28
	ADD	DI,270
	CMP	DI,80*400
	JNZ	MP2
	SUB	BP,800H
	SHR	TEMP,1
	JNB	MP1
	RET
MAPCOL	DW	0501H,0F02H,0F04H
	DW	0F01H,0502H
MAP	ENDP

RANDOM	PROC	NEAR
	MOV	AX,RND
	MOV	BX,AX
	ADD	AX,AX
	ADD	AX,AX
	ADD	AX,BX
	INC	AX
	MOV	RND,AX
	RET
RANDOM	ENDP

CANNON	PROC	NEAR
	CMP	CNNINF,0
	JNZ	CNN1
	TEST	KEYTBL+70H,0FFH
	JZ	CNN1
	MOV	SI,OFFSET CNNINF
	MOV	CL,RAY
	XOR	BH,BH
	MOV	[SI+4],CL
	MOV	BL,CL
	MOV	[SI+1],BH
	MOV	[SI+5],BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	SAR	AX,1
	MOV	[SI+10],AX
	MOV	AL,AH
	CBW
	SAR	AX,1
	SAR	AX,1
	SAR	AX,1
	ADD	AX,MANY
	MOV	[SI+6],AX
	XOR	BH,BH
	MOV	BL,CL
	ADD	BL,64
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	SAR	AX,1
	MOV	[SI+8],AX
	MOV	AL,AH
	CBW
	SAR	AX,1
	SAR	AX,1
	SAR	AX,1
	ADD	AX,MANX
	MOV	[SI+2],AX
	MOV	BYTE PTR [SI],82
	MOV	WORD PTR [SI+12],100H
	MOV	BYTE PTR [SI+14],120
	MOV	BYTE PTR [SI+15],0
CNN1:	RET
CANNON	ENDP

MOVCNN	PROC	NEAR
	MOV	SI,OFFSET CNNINF
	MOV	AL,[SI]
	DEC	AL
	JS	CNN1
	MOV	[SI],AL
	CMP	BYTE PTR [SI+15],1
	JZ	CNN1
	JB	MCNN2
	JMP	MCNN6
MCNN2:	MOV	BX,[SI+8]
	MOV	AL,BH
	CBW
	ADD	BL,[SI+1]
	ADC	AX,[SI+2]
	JS	MCNN5
	MOV	WORD PTR TEMP,AX
	MOV	DX,[SI+10]
	MOV	AL,DH
	CBW
	ADD	DL,[SI+5]
	ADC	AX,[SI+6]
	JNS	MCNNT5
MCNN5:	MOV	BYTE PTR [SI],0
	RET
MCNNT5:	MOV	BH,DL
	MOV	WORD PTR TEMP+2,AX
	MOV	BP,BX
	MOV	AL,[SI+14]
	CBW
	MOV	CX,[SI+12]
	MOV	DX,CX
	ADD	DX,AX
	JNS	MCNN7
	XOR	DX,DX
	JMP	MCNN4
MCNN7:	SUB	AL,3
	MOV	[SI+14],AL
	MOV	BL,TEMP+1
	MOV	BH,TEMP+3
	SHL	BL,1
	SHR	BX,1
	MOV	BL,[BX]+FIELD2
	ADD	BL,BL
	JB	MCNN1
	XOR	BH,BH
	ADD	BX,BX
	ADD	BX,BX
	MOV	BL,[BX]+BILDAT
	CMP	CH,BL
	JBE	MCNN3
	CMP	DH,BL
	JA	MCNN1
	MOV	DH,BL
	INC	DH
	XOR	DL,DL
MCNN4:	MOV	BYTE PTR [SI+15],1
MCNN1:	MOV	BX,BP
	MOV	[SI+1],BL
	MOV	[SI+5],BH
	MOV	BX,WORD PTR TEMP
	MOV	[SI+2],BX
	MOV	BX,WORD PTR TEMP+2
	MOV	[SI+6],BX
	MOV	[SI+12],DX
	RET
MCNN3:	MOV	BYTE PTR [SI+15],2
	TEST	BYTE PTR [SI+14],80H
	JNZ	MCNN9
	MOV	BYTE PTR [SI+14],0
	RET
MCNN9:	MOV	[SI+12],DX
	RET
MCNN6:	MOV	AL,[SI+14]
	CBW
	MOV	CX,[SI+12]
	ADD	CX,AX
	JS	MCNN8
	SUB	AL,3
	MOV	[SI+14],AL
	MOV	BL,[SI+3]
	MOV	BH,[SI+7]
	SHL	BL,1
	SHR	BX,1
	MOV	BL,[BX]+FIELD2
	ADD	BL,BL
	JB	MCNN10
	XOR	BH,BH
	ADD	BX,BX
	ADD	BX,BX
	MOV	BH,[BX]+BILDAT
	CMP	CH,BH
	JA	MCNN10
	MOV	BYTE PTR [SI+15],1
	INC	BH
	XOR	BL,BL
	MOV	[SI+12],BX
	RET
MCNN10:	MOV	[SI+12],CX
	RET
MCNN8:	MOV	WORD PTR [SI+12],0
	MOV	BYTE PTR [SI+15],1
	RET
MOVCNN	ENDP

DSPCNN	PROC	NEAR
	TEST	CNNINF,0FFH
	JZ	DCNN1
	MOV	AX,WORD PTR CNNINF+2
	SUB	AX,MANX
	CMP	AH,-20
	JL	DCNN1
	CMP	AH,20
	JGE	DCNN1
	MOV	CX,WORD PTR CNNINF+6
	SUB	CX,MANY
	CMP	CH,-20
	JL	DCNN1
	CMP	CH,20
	JGE	DCNN1
	CALL	CALC
	JNB	DCNN1
	MOV	AX,WORD PTR CNNINF+12
	DEC	AH
	JS	DCNN4
	MUL	DSPY1
	AND	DH,DH
	JNZ	DCNN1
	MOV	AL,AH
	MOV	AH,DL
	NEG	AX
	JMP	DCNN3
DCNN4:	NEG	AX
	MUL	DSPY1
	AND	DH,DH
	JNZ	DCNN1
	MOV	AL,AH
	MOV	AH,DL
DCNN3:	ADD	AX,3480H
	MOV	BP,DSPY1
	ADD	BP,3480H
	MOV	AL,DSPX1
	MOV	DL,71H
	MOV	ES,DSPSEG
	CALL	SPSET3
DCNN1:	RET
DCNN2:	OR	ES:[DI],DH
	RET
DSPCNN	ENDP

CALC	PROC	NEAR
	MOV	CALCX,AX
	MOV	CALCY,CX
	CALL	ATN
	MOV	BETA1,AL
	SUB	AL,RAY
	MOV	PHAI1,AL
	ADD	AL,31
	CMP	AL,63
	JB	CLC1
	RET
CLC1:	MOV	BL,PHAI1
	XOR	BH,BH
	MOV	DL,[BX]+TANTB2
	MOV	DSPX1,DL
	ADD	BL,40H
	ADD	BX,BX
	MOV	BX,[BX]+SINTBL
	MOV	AL,BETA1
	ADD	AL,32
	TEST	AL,40H
	JZ	CLC3
	MOV	AX,CALCY
	AND	AX,AX
	JNS	CLC2
	NEG	AX
CLC2:	MUL	BX
	MOV	BL,BETA1
	JMP	CLC5
CLC3:	MOV	AX,CALCX
	AND	AX,AX
	JNS	CLC4
	NEG	AX
CLC4:	MUL	BX
	MOV	BL,BETA1
	ADD	BL,40H
CLC5:	XOR	BH,BH
	ADD	BX,BX
	MOV	AX,[BX]+SINTBL
	AND	AX,AX
	JNS	CLC6
	NEG	AX
CLC6:	MOV	BX,DX
	XOR	DX,DX
	DIV	BX
	TEST	AH,0F8H
	JNZ	CLC7
	SHL	AX,1
	SHL	AX,1
	SHL	AX,1
	SHL	AX,1
	STC
	MOV	DSPY1,AX
	RET
CLC7:	MOV	DSPY1,7FFFH
	STC
	RET
CALC	ENDP

SPSET3	PROC	NEAR
	CMP	BP,4480H
	JAE	SP34
	CMP	BP,3880H
	JAE	SP33
	CMP	AH,100
	JAE	SP31
	CALL	SPSET2
SPSET4:	CMP	BP,[DI]+SCRN3D
	JBE	SP31
	CMP	BYTE PTR ES:[DI+2000H],DL
	JNZ	SP32
	OR	ES:[DI],DH
SP31:	RET
SP32:	MOV	ES:[DI],DH
	MOV	ES:[DI+2000H],DL
	RET
SP33:	CMP	AH,100
	JAE	SP31
	AND	AL,0FEH
	CALL	SPSET2
	MOV	BL,DH
	XOR	BH,BH
	MOV	DH,CS:[BX]+SPS3DT
	JMP	SPSET4
SPS3DT	DB	0,33H,66H,0
	DB	0CCH,0,0,0
	DB	0CCH
SP34:	DEC	AH
	CMP	AH,98
	JAE	SP31
	DEC	AL
	CMP	AL,158
	JAE	SP31
	MOV	BX,AX
	CALL	SPSET2
	SHR	BL,1
	RCL	BH,1
	MOV	BL,BH
	AND	BL,7
	XOR	BH,BH
	ADD	BX,BX
	ADD	BX,BX
	ADD	BX,BX
	LEA	SI,[BX]+SPS3D2
	LODS	CS:BYTE PTR [SI]
	AND	AL,AL
	JZ	SP35
	MOV	DH,AL
	CALL	SPSET4
SP35:	INC	DI
	INC	DI
	LODS	CS:BYTE PTR [SI]
	AND	AL,AL
	JZ	SP36
	MOV	DH,AL
	CALL	SPSET4
SP36:	ADD	DI,158
	LODS	CS:BYTE PTR [SI]
	AND	AL,AL
	JZ	SP37
	MOV	DH,AL
	CALL	SPSET4
SP37:	INC	DI
	INC	DI
	LODS	CS:BYTE PTR [SI]
	AND	AL,AL
	JZ	SP38
	MOV	DH,AL
	CALL	SPSET4
SP38:	RET
SPS3D2	DB	77H,7,0,0,70H,77H,0,0
	DB	0EEH,0EH,0,0,0E0H,0EEH,0,0
	DB	0CCH,0CH,11H,1,0C0H,0CCH,10H,11H
	DB	88H,8,33H,3,80H,88H,30H,33H
SPSET3	ENDP

FILENM	DB	'MISSION.DAT',0

CODE	ENDS

DATA	SEGMENT

FIELD	DB	4000H DUP(?)
FIELD2	DB	4000H DUP(?)
RAY	DB	?
BLINEF	DB	?
TEMP	DB	6 DUP(?)
BILADD	DW	?
BILX1	DW	?
BILY1	DW	?
BILX2	DW	?
BILY2	DW	?
PHAI1	DB	?
PHAI2	DB	?
DSPX1	DB	?
DSPX2	DB	?
DSPY1	DW	?
DSPY2	DW	?
BETA1	DB	?
BETA2	DB	?
DSPSEG	DW	?
KEYOFS	DW	?
KEYSEG	DW	?
	DW	?
MANX	DW	?
	DW	?
MANY	DW	?
FIREOK	DB	?
GCOUNT	DB	?
BULX	DW	?
BULY	DW	?
BULADD	DW	?
LTRNOK	DB	?
RTRNOK	DB	?
DATSEG	DW	?
RND	DW	?
MAPRES	DW	?
CALCX	DW	?
CALCY	DW	?
MAPFL	DB	?

	ORG	9000H
TANTBL	DW	20H DUP(?)
SINTBL	DW	100H DUP(?)
TANTB2	DB	100H DUP(?)
BOTTOM	DW	1E0H DUP(?)
BULINF	DB	200H DUP(?)
CNNINF	DB	100H DUP(?)
BILDAT	DB	400H DUP(?)
BILCHK	DB	80H DUP(?)
GRDATA	DB	80H DUP(?)
KEYTBL	DB	100H DUP(?)
SCRN3D	DW	1000H DUP(?)
ENMINF	DB	800H DUP(?)
ENMDAD	DW	100H DUP(?)

DATA	ENDS

	END	START
